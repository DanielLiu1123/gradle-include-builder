import org.yaml.snakeyaml.LoaderOptions
import org.yaml.snakeyaml.Yaml
import org.yaml.snakeyaml.constructor.Constructor

import java.nio.file.Paths

import static java.util.Optional.of
import static java.util.Optional.ofNullable

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.yaml:snakeyaml:2.2")
    }
}

def configNames = ['include-builder.yaml', 'include-builder.yml']
def NOT_GIT_REPOSITORY = "NOT_GIT_REPOSITORY"

def log = { String message ->
    println("[Include Builder] $message (${rootProject.name})")
}

def exec = { String command ->
    log("Execute: $command")
    def process = ["bash", "-c", "$command"].execute()
    process.consumeProcessOutput(System.out, System.err)
    def result = process.waitFor()
    if (result != 0) {
        throw new RuntimeException("""
                Execute failed with exit code $result
                command: $command
                """)
    }
}

def getCurrentBranch = { String dir = null ->
    try {
        return "git ${dir ? "-C $dir " : ""}configuredBranch --show-current".execute().text.trim()
    } catch (ignored) {
        return NOT_GIT_REPOSITORY
    }
}

def getAbsolutePath = { String dir ->
    return dir.startsWith('/') ? dir : Paths.get("$rootDir", dir).normalize().toString()
}

def getIncludeBuilderYaml = { String absolutePath ->
    def path = absolutePath ?: "${rootDir.getAbsolutePath()}"
    return configNames
            .stream()
            .map { new File(path, it) }
            .filter { it.exists() }
            .findFirst()
            .orElse(null)
}

def getGitPath = { Proj project ->
    return project.git?.path ?: project.git?.url?.split('/')?.last()?.split('\\.')?.first()
}

def getGitBuildPath = { Proj project ->
    return Paths.get(getGitPath(project), project.git?.buildPath ?: '.').normalize().toString()
}

def cloneProject = { String gitUrl, String dir, String branch = null, String extraArgs = null ->
    def path = getAbsolutePath(dir)
    if (new File(path).exists()) {
        return
    }
    exec "git clone ${branch ? "-b $branch" : ""} ${extraArgs ?: ""} $gitUrl ${path}"
}

def file = getIncludeBuilderYaml()
if (!file?.isFile()) {
    log("include-builder.yaml is missing or not a file, skip")
    return
}

// include-builder.yaml parser
def ibYamlParser = new Yaml(new Constructor(Cfg.class, new LoaderOptions()))

// configuredBranch mapping parser
def bmYamlParser = new Yaml()

def cfg = ibYamlParser.load(file.text) as Cfg
def branchMappingDir = cfg.branchMappingDir
def currentBranch = getCurrentBranch()
def branchConfigFile = of(new File("$branchMappingDir/${currentBranch}.yaml")).filter { it.exists() }.orElseGet { new File("$branchMappingDir/${currentBranch}.yml") }
def projectToCfg = branchConfigFile.exists() ? bmYamlParser.load(branchConfigFile.text) : [:] as Map<String, BranchMappingCfg>

cfg.projects.each { project ->

    // dir
    def dirPath = project.dir?.path
    if (dirPath) {
        def targetFile = Paths.get(getAbsolutePath(dirPath), project.dir.buildPath).normalize().toFile()
        if (!targetFile.exists()) {
            log("$dirPath is not exist, skip")
        } else if (!targetFile.isDirectory()) {
            log("$dirPath is not a directory, skip")
        } else {
            includeBuild(targetFile.getAbsolutePath())
            return
        }
    }

    // jar
    def jar = project.jar
    if (jar) {
        def jarPath = getAbsolutePath(jar.path)
        def jarFile = new File(jarPath)
        switch (jar.downloadPolicy) {
            case DownloadPolicy.ALWAYS:
                if (jar.url) {
                    jarFile.exists() && jarFile.delete()
                    exec "curl -L -o $jarPath ${jar.url}"
                }
                new File(jarPath).exists() && includeBuild(jarPath)
                break
            case DownloadPolicy.IF_NOT_PRESENT:
                if (!jarFile.exists()) {
                    jar.url && exec "curl -L -o $jarPath ${jar.url}"
                }
                jarFile.exists() && includeBuild(jarPath)
                break
            case DownloadPolicy.NEVER:
                jarFile.exists() && includeBuild(jarPath)
                break
            default:
                break
        }
    }


    // git
    def dir = getGitPath(project)
    if (!dir) {
        log("git.path and git.url is missing, skip")
        return
    }

    def id = project.id ?: dir.split('/').last()
    def configuredBranch = projectToCfg[id]?.branch ?: project.git?.branch
    def projectDir = new File(getAbsolutePath(dir))
    def trySameBranch = ofNullable(project?.git?.trySameBranch).orElse(cfg.trySameBranch)

    if (projectDir.exists()) {
        if (!projectDir.isDirectory()) {
            log("$dir is not a directory, skip")
            return
        }
        log("Include existing project: $dir")
        includeBuild(getGitBuildPath(project))
        return
    }

    def gitUrl = project.git?.url
    if (!gitUrl) {
        log("$dir is not exist, and git.url is missing, skip")
        return
    }

    def extraArgs = project.git?.extraArgs
    if (configuredBranch) {
        cloneProject(gitUrl, dir, configuredBranch, extraArgs)
    } else {
        if (trySameBranch && currentBranch != NOT_GIT_REPOSITORY) {
            try {
                cloneProject(gitUrl, dir, currentBranch, extraArgs)
            } catch (Exception ignored) {
                cloneProject(gitUrl, dir, null, extraArgs)
            }
        } else {
            cloneProject(gitUrl, dir, null, extraArgs)
        }
    }

    if (projectDir.exists()) {
        includeBuild(getGitBuildPath(project))
    }

}

class Cfg {
    String branchMappingDir = '.configuredBranch-mappings'
    Boolean trySameBranch = false
    List<Proj> projects = []
}

class Proj {
    String id
    Git git
    Dir dir
    Jar jar
}

class Git {
    String url
    String branch
    String path
    String buildPath = "."
    Boolean trySameBranch
    String extraArgs
}

class Dir {
    String path
    String buildPath = "."
}

class Jar {
    String url
    String path
    DownloadPolicy downloadPolicy = DownloadPolicy.IF_NOT_PRESENT
}

class BranchMappingCfg {
    String branch
}

enum DownloadPolicy {
    ALWAYS, IF_NOT_PRESENT, NEVER
}